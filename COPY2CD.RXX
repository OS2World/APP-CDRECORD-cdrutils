/********************************************************************/
/*                                                                  */
/* COPY2CD: Version 1.30                                            */
/* Author: Michel SUCH. Email: such.michel@wanadoo.fr               */
/*                                                                  */
/* Copy the content of a directory to cd                            */
/* using CDRECORD and MKISOFS.                                      */
/*                                                                  */
/* This program requires CDRECORD/2 installed on your machine       */
/*                                                                  */
/* CDRECORD/2 can be obtained on HOBBES: http://hobbes.nmsu.edu     */
/*                                                                  */
/* CDRECORD.EXE and MKISOFS.EXE must be located in a directory      */
/* defined in the path variable of the CONFIG.SYS file              */
/* or in the directory containing the LEECHMP3 program.             */
/********************************************************************/

   parse arg parms
   call get_parms
   call presentation
   call init_prog
   call show_devices
   call perform_copy
exit rc


init_prog:

/*------------------- Init Rexxutil-----------------------------------*/
  If RXFUNCQUERY('SysLoadFuncs') Then Do
    Call rxfuncadd 'SysLoadFuncs', 'REXXUTIL', 'SYSLOADFUNCS'
    Call sysloadfuncs
  End
   parse source . . progname .
   progdir = filespec('d', progname) || filespec('p', progname)

   /* Check if we have all appropriate programs */
   mkisofs  = find_prog("MKISOFS2.EXE")
   if mkisofs <> '' then mkiextra = "--notitle"
   else do
      mkisofs  = find_prog("MKISOFS.EXE")
      if mkisofs  = '' then do
         say "Cannot find program MKISOFS.EXE or MKISOFS2.EXE"
         exit 2
      end
      mkiextra = ''
   end

   cdrecord = find_prog("CDRECORD2.EXE")
   if cdrecord <> '' then cdrextra = cdrextra '-n'
   else do
      cdrecord = find_prog("CDRECORD.EXE")
      if cdrecord = '' then do
         say "Cannot find program CDRECORD.EXE or CDRECORD2.EXE"
         exit 2
      end
   end

   call get_devices(cdrecord)

   if cdrw.id = '' then do
     say "Cannot find CD-RECORDER"
     exit -2
   end
return /* init_prog */


presentation:

  parse source . . me .
  parse value(filespec('n', me)) with progname'.'.
   say progname "Version 1.30"
   say "Author: Michel SUCH, Email: such.michel@wanadoo.fr"
   say ""
   if help = 1 then do
     say "Syntax :" progname "[dirname] [options]"
     say ""
     say "dirname : name of the directory to copy (default is current directory"
     say "-b - Tries to blank cd before copying"
     say "-n - No pause before starting copy"
     say "-o - Tries to overburn cd"
     say "-t - Test mode, cd not really written"
     say "-h - Displays this help"
     exit
   end
   else do
     say "Type" progname "-h to get help"
   end
return /* presentation */


show_devices:

  if noprompt = 1 then return
  say "Copying directory" base "to CD"
   say "CD-Rw  =" cdrw.info
   say "Press Enter to continue, any other key to cancel"
   if sysgetkey('noecho') = '0d'x then return
   else do
     say "Copy cancelled"
     exit
   end
return /* show_devices */


get_parms:

  base = ''
  cdrextra = ''
  do i = 1 to words(parms)
    parm = word(parms, i)
    key = translate(parm)
    select
      when key = '-B' then cdrextra = cdrextra "blank=fast"
      when key = "-O" then cdrextra = cdrextra "-overburn"
      when key = "-T" then cdrextra = cdrextra "-dummy"
      when key = '-N' then noprompt = 1
      when key = '-H' then help = 1
      otherwise do
        if base = '' then base = parm
        else do
          say "Invalid parameter" parm
          exit 2
        end
      end
    end
  end

   startdir = directory()
   if base = '' then base = startdir
   else do
      dir = directory(base)
      if dir = '' then do
         say "Directory" base "does not exist"
         exit 2
      end
   end
   basefile = '"'translate(base, '/', '\')'"'
return /* get_parms */


perform_copy:

   charset = get_codepage()
   do forever
     "call" mkisofs mkiextra "-f -pad -quiet -l -L",
     "-input-charset" charset "-output-charset" charset,
     "-V" '"LEECHMP3DSC"' "-J" basefile,
     "|" cdrecord  "fs=16m" cdrw.id cdrw.speed cdrextra "-v -eject -pad -waiti -data",
     get_tsize(basefile) "-"
     select
       when rc = 0 then do
         say "CD recorded successfully"
         leave
       end

       when rc = 2 then do
         say "Error writing on cd"
         leave
       end

       when rc = 255 then do
         call beep 440, 100
         say "Insert a blank cd in the cd recorder"
         say "Press enter to continue, any other key to cancel"
         if sysgetkey("noecho") = '0d'x then iterate
         else do
           say "Copy cancelled"
           leave
         end
       end

       otherwise do
         say "Error reading files rc="rc
         leave
       end
     end /* select */
   end /* do forever */
return /* perform_copy */


get_codepage: procedure

   /* Setup a local queue to avoid conflicts with other process */
   cpqueue = rxqueue('create')
   oldq = rxqueue('set', cpqueue)

   "@chcp 2>nul | rxqueue" cpqueue
   pull cp
   do queued()
      pull .
   end
   oldq = rxqueue('set', oldq)
   cpqueue = rxqueue('delete', cpqueue)
return "cp" || word(cp, words(cp)) /* get_codepage */


get_tsize:
procedure expose mkisofs mkiextra

   parse arg basefile
   /* Setup a local queue to avoid conflicts with other process */
   tsqueue = rxqueue('create')
   oldq = rxqueue('set', tsqueue)

   "call" mkisofs mkiextra "-f -quiet -l -L -pad -print-size -J" basefile "2>nul | rxqueue" tsqueue
   do queued()
      parse pull tsize
   end
   oldq = rxqueue('set', oldq)
   tsqueue = rxqueue('delete', tsqueue)
return "-tsize="tsize"s" /* get_tsize */


find_prog:
procedure

   /* First, search in the program's directory */
   parse source . . me .
   pgm = stream(filespec('d', me) || filespec('p', me) || arg(1), 'c', 'query exist')
   if pgm = '' then do /* search in the path environment variable */
      pgm = syssearchpath( 'path', arg(1))
   end
return pgm /* find_prog */

get_devices: procedure expose cdrom. cdrw.

  parse arg cdrecord /* cdrecord.exe or cdrecord2.exe full path */
  cdrom. = ''
  cdrw. = ''
   /* Setup a local queue to avoid conflicts with other process */
   cdrqueue = rxqueue('create')
   oldq = rxqueue('set', cdrqueue)
   idev = 0

   "call" cdrecord "-scanbus 2>nul | rxqueue" cdrqueue

   /* Select all cd-rom devices */
   do queued()
      parse pull line
      if pos("Removable CD-ROM", line) = 0 then iterate
      idev = idev + 1
      line = strip(translate(line, '', '09'x))
      parse var line cd.idev' '."'"vendor.idev"'"."'"model.idev"'".
   end

   /* Get needed info for each cd-rom */
   do i = 1 to idev
     call get_devinfo cd.i vendor.i || model.i
   end
   oldq = rxqueue('set', oldq)
   cdrqueue = rxqueue('delete', cdrqueue)

   if cdrom.id = '' then do /* no cd-rom, only a cd-rw */
     cdrom.info = cdrw.info
     cdrom.speed = ''
     cdrom.id = cdrw.id
   end
return /* get_devices */


get_devinfo:

  parse arg dev cdinfo
   rs = 0
   ws = 0

   "call" cdrecord "dev="dev "-prcap  2>nul | rxqueue" cdrqueue
   do queued()
     parse pull ln
     key = translate(subword(ln, 1, 3))
     select
       when key = "MAXIMUM WRITE SPEED:" then do
         ws = format_speed(word(ln, 4))
       end

       when key = "MAXIMUM READ  SPEED:" then do
         rs = format_speed(word(ln, 4))
       end

       otherwise nop
     end /* select */
   end /* do queued() */

   if ws = 0 then do /* cd-rom */
     if cdrom.info <> '' then return /* get only the first cd-rom */
     if rs > 0 then do
       cdrom.speed = "speed="rs
     end
     cdrom.id = "dev="dev
     cdrom.info = cdinfo cdrom.id cdrom.speed
   end
   else do /* CD-RW */
     if cdrw.info <> '' then return /* get only the first cd-rw */
     cdrw.speed = "speed="ws
     cdrw.id = "dev="dev
     cdrw.info = cdinfo cdrw.id cdrw.speed
   end
return /* get_devinfo */


format_speed: procedure

  return  format(arg(1) / 176, , 0)
/* End COPY2CD */
